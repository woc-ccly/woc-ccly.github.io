prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :main(?ns ?prefix) {
    "<section>"
        "<h2>Axiomatization</h2>"
        "<section>"
            "<h3>Classes</h3>"
            st:call-template(:classes, ?ns, ?prefix)
        "</section>"
        "<section>"
            "<h3>Object Properties</h3>"
            st:call-template(:properties, ?ns, ?prefix, owl:ObjectProperty)
        "</section>"
        "<section>"
            "<h3>Datatype Properties</h3>"
            st:call-template(:properties, ?ns, ?prefix, owl:DatatypeProperty)
        "</section>"
        st:call-template(:individuals-section, ?ns, ?prefix)
    "</section>"
} where {}

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix schema: <http://schema.org/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :individuals-section(?ns ?prefix) {
    "<section>"
        "<h3>Named Individuals</h3>"
        st:call-template(:individuals, ?ns, ?prefix)
    "</section>"
} where {
    filter exists {
        ?i a ?c .
        ?c rdfs:subClassOf schema:Enumeration .
        filter (strstarts(str(?i), str(?ns)))
    }
}

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :classes(?ns ?prefix) {
    "<section>"
        st:call-template(:term-header, ?c, ?prefix)
        "<table class=\"def\">"
            "<tbody>"
                st:call-template(:class-superclass, ?c, ?prefix)
                st:call-template(:class-subclasses, ?c, ?prefix)
                st:call-template(:class-domain-all, ?c, ?prefix)
                st:call-template(:class-range-all, ?c, ?prefix)
                st:call-template(:enumeration-members, ?c, ?prefix)
            "</tbody>"
        "</table>"
    "</section>"
} where {
    ?c a owl:Class ;
       rdfs:comment ?desc .
    filter (strstarts(str(?c), str(?ns)))
    bind (strafter(str(?c), "#") as ?label)
} order by ?c

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :class-superclass(?c ?prefix) {
    "<tr>"
        "<td>Sub-class of</td>"
        format {
            "<td>%s</td>"
            st:call-template(:term-link, ?sc, ?prefix)
        }
    "</tr>"
} where {
    ?c rdfs:subClassOf ?sc .
    ?sc a owl:Class .
}

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :class-subclasses(?c ?prefix) {
    "<tr>"
        "<td>Super-class of</td>"
        format {
            "<td>%s</td>"
            st:call-template(:class-subclass, ?c, ?prefix)
        }
    "</tr>"
} where {
    filter exists {
        ?sc rdfs:subClassOf ?c .
        ?sc a owl:Class .
    }
}

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :class-subclass(?c ?prefix) {
    st:call-template(:term-link, ?sc, ?prefix) ; separator = "<br>"
} where {
    ?sc rdfs:subClassOf ?c .
    ?sc a owl:Class .
} order by ?sc

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix schema: <http://schema.org/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :class-domain-all(?c ?prefix) {
    "<tr>"
        "<td>In the domainÂ of</td>"
        "<td>" st:call-template(:class-domain, ?c, ?prefix) "</td>"
    "</tr>"
} where {
    filter exists {
        ?p schema:domainIncludes ?c .
    }
}

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix schema: <http://schema.org/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :class-domain(?c ?prefix) {
    st:call-template(:term-link, ?p, ?prefix) ; separator = "<br>"
} where {
    ?p schema:domainIncludes ?c .
} order by ?p

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix schema: <http://schema.org/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :class-range-all(?c ?prefix) {
    "<tr>"
        "<td>In the range of</td>"
        "<td>" st:call-template(:class-range, ?c, ?prefix) "</td>"
    "</tr>"
} where {
    filter exists {
        ?p schema:rangeIncludes ?c .
    }
}

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix schema: <http://schema.org/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :class-range(?c ?prefix) {
    st:call-template(:term-link, ?p, ?prefix) ; separator = "<br>"
} where {
    ?p schema:rangeIncludes ?c .
} order by ?p

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix schema: <http://schema.org/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :enumeration-members(?c ?prefix) {
    "<tr>"
        "<td>Enumeration members</td>"
        "<td>" st:call-template(:enumeration-member, ?c, ?prefix) "</td>"
    "</tr>"
} where {
    ?c rdfs:subClassOf schema:Enumeration .
}

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix schema: <http://schema.org/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :enumeration-member(?c ?prefix) {
    st:call-template(:term-link, ?member, ?prefix) ; separator = "<br>"
} where {
    ?c rdfs:subClassOf schema:Enumeration .
    ?member a ?c .
} order by ?member

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :properties(?ns ?prefix ?type) {
    "<section>"
        st:call-template(:term-header, ?p, ?prefix)
        "<table class=\"def\">"
            "<tbody>"
                st:call-template(:property-domain-all, ?p, ?prefix)
                st:call-template(:property-range-all, ?p, ?prefix)
            "</tbody>"
        "</table>"
    "</section>"
} where {
    ?p a ?type ;
       rdfs:comment ?desc .
    filter (strstarts(str(?p), str(?ns)))
    bind (strafter(str(?p), "#") as ?label)
} order by ?p

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix schema: <http://schema.org/>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :property-domain-all(?p ?prefix) {
    "<tr>"
        "<td>Domain includes</td>"
        format {
            "<td>%s</td>"
            st:call-template(:property-domain, ?p, ?prefix)
        }
    "</tr>"
} where {
    filter exists {
        ?p schema:domainIncludes ?dom .
    }
}

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix schema: <http://schema.org/>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :property-domain(?p ?prefix) {
    st:call-template(:term-link, ?dom, ?prefix) ; separator = "<br>"
} where {
    { ?p rdfs:domain ?dom . }
    union
    { ?p schema:domainIncludes ?dom . }
    filter (isIRI(?dom))
} order by ?dom

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix schema: <http://schema.org/>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :property-range-all(?p ?prefix) {
    "<tr>"
        "<td>Range includes</td>"
        format {
            "<td>%s</td>"
            st:call-template(:property-range, ?p, ?prefix)
        }
    "</tr>"
} where {
    filter exists {
        ?p schema:rangeIncludes ?range .
    }
}

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix schema: <http://schema.org/>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :property-range(?p ?prefix) {
    st:call-template(:term-link, ?range, ?prefix) ; separator = "<br>"
} where {
    { ?p rdfs:range ?range . }
    union
    { ?p schema:rangeIncludes ?range . }
    filter (isIRI(?range))
} order by ?range

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix schema: <http://schema.org/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :individuals(?ns ?prefix) {
    "<section>"
        st:call-template(:term-header, ?i, ?prefix)
        "<table class=\"def\">"
            "<tbody>"
                "<tr>"
                    "<td>Instance of</td>"
                    "<td>" st:call-template(:term-link, ?c, ?prefix) "</td>"
                "</tr>"
            "</tbody>"
        "</table>"
    "</section>"
} where {
    ?i a ?c .
    ?c rdfs:subClassOf schema:Enumeration .
    filter (strstarts(str(?i), str(?ns)))
}

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :term-header(?term ?prefix) {
    format { "<h4>%s</h4>" ?label }
    format { "<p>IRI: <code>%s</code></p>" ?term }
    format { "<span>%s</span>" ?desc }
} where {
    ?term rdfs:comment ?desc
    optional { ?term rdfs:label ?l }
    bind (if(bound(?l), ?l, strafter(str(?term), "#")) as ?label)
}

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix schema: <http://schema.org/>
prefix vann: <http://purl.org/vocab/vann/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>
template :term-link(?term ?defaultPrefix) {
    format {
        "<code><a href=\"%s\">%s</a></code>"
        if(?prefix = ?defaultPrefix,
            concat("#", lcase(?label)),
            str(?term))
        if(?curie,
            # FIXME schema.org label not correctly extracted if in WHERE clause?
            concat(?prefix, ":", if(strstarts(str(?term), str(schema:)), strafter(str(?term), str(schema:)), ?label)),
            ?label)
    }
} where {
    optional { ?term rdfs:label ?l }
    bind (if(bound(?l), ?l, strafter(str(?term), "#")) as ?label)
    optional {
        ?o a owl:Ontology ;
            vann:preferredNamespacePrefix ?prefix .
        filter (strstarts(str(?term), str(?o)))
    }
    optional {
        bind ("schema" as ?prefix)
        filter (strstarts(str(?term), str(schema:)))
    }
    bind (bound(?prefix) as ?curie)
}